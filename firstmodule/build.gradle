if (isModule.toBoolean()) {
    apply plugin: 'com.android.application'
} else {
    apply plugin: 'com.android.library'
}

android {
    compileSdkVersion build_version.compileSdkVersion

    defaultConfig {
        applicationId "com.nel.firstmodule"
        minSdkVersion build_version.minSdkVersion
        targetSdkVersion build_version.targetSdkVersion
        versionCode build_version.versionCode
        versionName build_version.versionName

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        vectorDrawables.useSupportLibrary = true

    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        main {
            if (isModule.toBoolean()) {
                manifest.srcFile 'src/main/module/AndroidManifest.xml'
            } else {
                manifest.srcFile 'src/main/AndroidManifest.xml'

                //集成开发模式下排除debug文件夹中的所有Java文件
                java {
                    exclude 'debug/**'
                }
            }
        }
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation "com.android.support:appcompat-v7:$support_version.supportLibriaryVersion"
    implementation "com.android.support:design:$support_version.supportLibriaryVersion"
    implementation "com.android.support:support-vector-drawable:$support_version.supportLibriaryVersion"
    implementation "com.android.support.constraint:constraint-layout:$support_version.supportConstraintVersion"
    testImplementation "junit:junit:$test_version.junitVersion"
    androidTestImplementation "com.android.support.test:runner:$test_version.runnerVersion"
    androidTestImplementation "com.android.support.test.espresso:espresso-core:$test_version.espressoVersion"
}
